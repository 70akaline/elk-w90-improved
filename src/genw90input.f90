
! Copyright (C) 2015 Jon Lafuente and Manh Duc Le, 2017-18 Arsenii Gerasimov,
! Yaroslav Kvashnin and Lars Nordstrom.
! This file is distributed under the terms of the GNU General Public License.
! See the file COPYING for license details.

!BOP
! !ROUTINE: genw90input
! !INTERFACE:
subroutine genw90input
! !USES:
use modmain
use modw90
use modw90overlap
use modmpi
use modomp
! !DESCRIPTION:
!   Writes the Mmn and Amn matrices required for Wannier90 to file.
!
! !REVISION HISTORY:
!   Created March 2015 (Jon Lafuente and Manh Duc Le)
!   Modified July 2017 (Arsenii Gerasimov)
!EOP
!BOC

implicit none
! local variables
integer jk,n,m,ig,is
integer ikp,inn
integer ist
integer nthd
real(8) gqc
! allocatable arrays
integer,    allocatable :: igpig(:,:),igpqig(:,:)
integer,    allocatable :: ngp(:),ngpq(:)
real(8),    allocatable :: jlgqr(:,:)
real(8),    allocatable :: evalsv_(:)
complex(8), allocatable :: ylmgq(:),sfacgq(:)
complex(8), allocatable :: expmt(:,:)
complex(8), allocatable :: mmn(:,:,:,:)
complex(8), allocatable :: spn_x(:,:),spn_y(:,:),spn_z(:,:)
complex(8), allocatable :: wfmt(:,:,:,:),wfir(:,:,:)
complex(8), allocatable :: wfmtq(:,:,:,:),wfirq(:,:,:)
! automatic arrays
real(8)        vgqc(3),vec_0(3),bqvec(3),bqc(3),vkql(3)
character(10)  dat,tim
character(256) filename
!-------------------------------------------------------------------------------
!
call initw90

! Open outputting files
!   Open Mmn-file
filename = trim(wann_seedname)//'.mmn'
open(500,file=filename,action='WRITE',form='FORMATTED')
call date_and_time(date=dat,time=tim)
!   Mmn-file header
write(500,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                          dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
write(500,'(3I8)') wann_nband,nkpt,wann_nntot
!   Open Spn-file
if ( nspinor .eq. 2 ) then
  filename = trim(wann_seedname)//'.spn'
  open(502,file=filename,action='WRITE',form='FORMATTED')
  !   Spn-file header
  write(502,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                            dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
  write(502,'(3I8)') wann_nband,nkpt
end if

! Write <seedname>.eig
allocate(evalsv_(nstsv))
filename = trim(wann_seedname)//'.eig'
open(50,file=filename,action='WRITE',form='FORMATTED')
do ikp = 1,nkpt
  call getevalsv(filext,ikp,vkl(:,ikp),evalsv_)
  do ig = 1,wann_nband
    ist = wann_bands(ig) ! AG :
    write(50,'(2I12,G18.10)') ig,ikp,( evalsv_(ist)-efermi )*27.211385 ! Convert to eV
  end do
end do
close(50)
deallocate(evalsv_)

if( nspinor .eq. 2 ) then
  allocate(spn_x(nkpt,(wann_nband*( wann_nband + 1 ))/2))
  allocate(spn_y(nkpt,(wann_nband*( wann_nband + 1 ))/2))
  allocate(spn_z(nkpt,(wann_nband*( wann_nband + 1 ))/2))
end if

if( mp_mpi ) then
  write(*,*)
  write(*,*) " Info(Wannier): Mmn calculation"
end if

ngrf = 1 ! Corresponds to expmt
! Synchronise MPI processes
call mpi_barrier(mpicom,ierror)
! Loop over reduced k-point set
call omp_hold(nkpt/np_mpi,nthd)
!$OMP PARALLEL DEFAULT(SHARED) &
!$OMP PRIVATE(ikp,n,m,inn,jk,bqvec,bqc,mmn,vkql) &
!$OMP PRIVATE(ngp,ngpq,igpig,igpqig) &
!$OMP PRIVATE(wfmt,wfir,wfmtq,wfirq) &
!$OMP PRIVATE(vgqc,gqc,jlgqr,ylmgq,sfacgq,expmt) &
!$OMP NUM_THREADS(nthd)

allocate(wfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfir(ngtot,nspinor,wann_nband))
allocate(jlgqr(njcmax,nspecies))
allocate(ylmgq(lmmaxo),sfacgq(natmtot))
allocate(expmt(npcmtmax,natmtot))
allocate(wfmtq(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfirq(ngtot,nspinor,wann_nband))
allocate(mmn(wann_nband,nspinor,wann_nband,nspinor))
allocate(igpig(ngkmax,nspnfv))
allocate(igpqig(ngkmax,nspnfv))
allocate(ngp(nspnfv),ngpq(nspnfv))

!$OMP DO
do ikp = 1,nkpt

  call genwfsvp(.false.,.false.,wann_nband,wann_bands,ngridg,igfft,vkl(:,ikp),ngp,igpig,wfmt,ngtot,wfir)

  ! Loop over ikp+b points
  do inn = 1,wann_nntot
    jk = nnlist(ikp,inn)
    bqvec = nncell(:,ikp,inn) + vkl(:,jk) - vkl(:,ikp)

    ! b-vector in Cartesian coordinates
    call r3mv(bvec,bqvec,bqc)
    ! Generate phase factor function exp(ibr) in the muffin-tins
    call gengqrf(bqc,vgqc,gqc,jlgqr,ylmgq,sfacgq)
    call genexpmt(1,jlgqr,ylmgq,1,sfacgq,expmt)

    ! k+b-vector in lattice coordinates
    vkql = vkl(:,ikp) + bqvec
    call genwfsvp(.false.,.false.,wann_nband,wann_bands,ngridg,igfft,vkql,ngpq,igpqig,wfmtq,ngtot,wfirq)

    ! Compute Mmn
    mmn = cmplx(0.d0,0.d0,kind=8)
    call genw90overlap(wfmt,wfir,wann_nband,wfmtq,wfirq,mmn,expmt)

  !$OMP CRITICAL(genw90input_)
    ! Write Mmn matrix elements
    write(500,'(5I8)') ikp,jk,nncell(:,ikp,inn)
    do n = 1,wann_nband
      do m = 1,wann_nband
        if ( nspinor .eq. 1 ) then
          write(500,'(2G18.10)') dble(mmn(m,1,n,1)),&
                                aimag(mmn(m,1,n,1))
        else
          write(500,'(2G18.10)') dble(mmn(m,1,n,1) + mmn(m,2,n,2)),&
                                aimag(mmn(m,1,n,1) + mmn(m,2,n,2))
        end if
      end do
    end do
  !$OMP END CRITICAL(genw90input_)

  end do ! End loop over b points

  ! If wavefunctions are spinors, compute spn-matrix by default
  if( nspinor .eq. 2 ) then
    vec_0(1) = 0.d0; vec_0(2) = 0.d0; vec_0(3) = 0.d0
    call gengqrf(vec_0,vgqc,gqc,jlgqr,ylmgq,sfacgq)
    call genexpmt(1,jlgqr,ylmgq,1,sfacgq,expmt)

    mmn = cmplx(0.d0,0.d0,kind=8)
    call genw90overlap(wfmt,wfir,wann_nband,wfmt,wfir,mmn,expmt)

  !$OMP CRITICAL(genw90input_)
    is = 1
    do m = 1,wann_nband
      do n = 1,m
        spn_x(ikp,is) = mmn(n,1,m,2) + mmn(n,2,m,1)
        spn_y(ikp,is) = cmplx(0.d0,1.d0,kind=8)*( mmn(n,2,m,1) - mmn(n,1,m,2) )
        spn_z(ikp,is) = mmn(n,1,m,1) - mmn(n,2,m,2)
        is = is + 1
      end do
    end do
  !$OMP END CRITICAL(genw90input_)
  end if

!$OMP CRITICAL(genw90input_)
  if ( mp_mpi ) then
    write(*,'("    Info(Wannier Mmn): completed ",I6," of ",I6," k-points")')&
                                                                   &ikp,nkpt
  end if
!$OMP END CRITICAL(genw90input_)

end do ! End loop over k points
!$OMP END DO

deallocate(wfmt,wfir)
deallocate(jlgqr,ylmgq,sfacgq)
deallocate(igpig,igpqig)
deallocate(ngp,ngpq)
deallocate(expmt)
deallocate(wfmtq,wfirq)
deallocate(mmn)

!$OMP END PARALLEL
call omp_free(nthd)

call genw90amn

if ( mp_mpi ) then
  write(*,*)
  write(*,*) " Info(Wannier): Mmn and Amn matrices have been computed"
end if

! Close <seedname>.mmn
close(500)

! Write <seedname>.spn
if( nspinor .eq. 2 ) then
  do ikp = 1,nkpt
    do is = 1,(wann_nband*( wann_nband + 1 ))/2
      write(502,'(2G18.10)') spn_x(ikp,is)
      write(502,'(2G18.10)') spn_y(ikp,is)
      write(502,'(2G18.10)') spn_z(ikp,is)
    end do
  end do

  ! Close <seedname>.spn
  close(502)

  if( mp_mpi ) then
    write(*,*)
    write(*,*) " Info(Wannier): matrix elements of S between Bloch states &
                                                             &are computed"
  end if
  deallocate(spn_x,spn_y,spn_z)
end if


deallocate(wann_atomsymb,wann_atompos)
deallocate(nnlist,nncell)
deallocate(wann_proj_site)
deallocate(wann_proj_l,wann_proj_m,wann_proj_radial)
deallocate(wann_proj_zaxis,wann_proj_xaxis)
deallocate(wann_proj_zona)
deallocate(wann_proj_exclude_bands_lib)
deallocate(wann_proj_spin)
deallocate(wann_proj_quantdir)
deallocate(wann_proj_isrand)

if ( mp_mpi ) then
  write(*,*)
  write(*,*) " Info(Wannier): Wannierization completed"
end if

reducek = reducek0

end subroutine genw90input
!EOC

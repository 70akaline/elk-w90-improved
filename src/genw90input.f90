! Copyright (C) 2015 Jon Lafuente and Manh Duc Le, 2017 Arsenii Gerasimov
! This file is distributed under the terms of the GNU General Public License.
! See the file COPYING for license details.

!BOP
! !ROUTINE: writew90mmn
! !INTERFACE:
subroutine genw90input
! !USES:
use modmain
use modw90
use modw90overlap
use modmpi
use modomp
! !DESCRIPTION:
!   Writes the Mmn and Amn matrices required for Wannier90 to file.
!
! !REVISION HISTORY:
!   Created March 2015 (Jon Lafuente and Manh Duc Le)
!   Upgraded July 2017 (Arsenii Gerasimov)
!EOP
!BOC

implicit none
! local variables
integer jk,n,m,ig,is,i,j,nrc,nrci
integer ikp,inn
integer ist
integer redkfil,nstsv_,recl
logical exists
real(8) t1
integer nst,nthd
real(8) vgqc(3),gqc
! allocatable arrays
real(8),    allocatable :: jlgqr(:,:)
complex(8), allocatable :: ylmgq(:),sfacgq(:)
complex(8), allocatable :: expmt(:,:)
complex(8), allocatable :: mmn(:,:,:,:)
complex(8), allocatable :: spn_x(:,:),spn_y(:,:),spn_z(:,:)
integer,    allocatable :: igpig(:,:),igpqig(:,:)
complex(8), allocatable :: wfmt(:,:,:,:),wfir(:,:,:)
complex(8), allocatable :: wfmtq(:,:,:,:),wfirq(:,:,:)
real(8),    allocatable :: evalsv_(:)
integer,    allocatable :: ngp(:),ngpq(:)
complex(8), allocatable :: zwfmt(:,:,:,:)
complex(8), allocatable :: zrhomt(:,:)
complex(8), allocatable :: zrhoir(:)
! automatic arrays
real(8)                 :: bqvec(3),bqc(3),vkql(3)
character(256)             filename
character(8)            :: fmt_ikp,fmt_is ! format descriptor
character(8)            :: ikp_str
character(8)            :: is_str
character(10)              dat,tim
real(8)                    vkl_(3),vec_0(3)
!integer           ngp(nspnfv),ngpq(nspnfv)

reducek0=reducek ! if reducek=1 was used in ground state calculations,
                 ! need to regenerate the eigenvectors set for the full BZ.
reducek=0

! generate all nesessary parameters and call libwannier setup
call setupw90lib

! open outputting files
! Mmn
filename = trim(wann_seedname)//'.mmn'
open(500,file=filename,action='WRITE',form='FORMATTED')
  call date_and_time(date=dat,time=tim)
  write(500,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                            dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
  write(500,'(3I8)') wann_nband,nkpt,wann_nntot

if (nspinor.eq.2) then
  ! spn
  filename = trim(wann_seedname)//'.spn'
  open(502,file=filename,action='WRITE',form='FORMATTED')
  write(502,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                            dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
  write(502,'(3I8)') wann_nband,nkpt
end if

! read density and potentials from file
call readstate
! Fourier transform Kohn-Sham potential to G-space
call genvsig
! read Fermi energy from a file
call readfermi
! find the new linearisation energies
call linengy
! generate the APW radial functions
call genapwfr
! generate the local-orbital radial functions
call genlofr

! check that EVECSV.OUT has all necessary k-points
allocate(evalsv_(nstsv))
redkfil=0
inquire(iolength=recl) vkl_,nstsv_,evalsv_
do ikp=1,nkpt
  exists=.false.
  t1=9.d99
  inquire(file='EVALSV'//trim(filext),exist=exists)
  if(exists) then
    open(70,file='EVALSV'//trim(filext),action='READ',form='UNFORMATTED', &
        access='DIRECT',recl=recl,err=101)
    read(70,rec=ikp,err=101) vkl_,nstsv_,evalsv_
    close(70)
    t1=abs(vkl(1,ikp)-vkl_(1))+abs(vkl(2,ikp)-vkl_(2))+abs(vkl(3,ikp)-vkl_(3))
  end if
101 continue
  if (.not.exists.or.t1.gt.epslat.or.nstsv.ne.nstsv_) then
    redkfil=1
    exit
  end if
end do

! If kpoint not found in saved eigen-values/vectors, then need to recompute EVEC*OUT.
if (redkfil.ne.0) then
  if(mp_mpi) then 
    write(*,*)
    write(*,*) "Info(Wannier): Saved k-points don't contain all required &
                                                               &k-points."
    write(*,*) "Info(Wannier): Recalculating eigenvalues..."
  end if
  ! compute the overlap radial integrals
  call olprad
  ! compute the Hamiltonian radial integrals
  call hmlrad
  ! generate the spin-orbit coupling radial functions
  call gensocfr
  ! generate the first- and second-variational eigenvectors and eigenvalues
  call genevfsv
  if(mp_mpi) then
    write(*,*) "Info(Wannier): Eigenvalues recalculated"
  end if
end if

! write seedname.eig file for wannier90
filename = trim(wann_seedname)//'.eig'
open(50,file=filename,action='WRITE',form='FORMATTED')
do ikp=1,nkpt
  call getevalsv(filext,ikp,vkl(:,ikp),evalsv_)
  do ig=1,wann_nband
    ist=wann_bands(ig) ! AG :
    write(50,'(2I12,G18.10)') ig,ikp,(evalsv_(ist)-efermi)*27.211385 ! convert to eV
  end do
end do
close(50)
deallocate(evalsv_)

if(nspinor.eq.2) then
  allocate(spn_x(nkpt,(wann_nband*(wann_nband+1))/2))
  allocate(spn_y(nkpt,(wann_nband*(wann_nband+1))/2))
  allocate(spn_z(nkpt,(wann_nband*(wann_nband+1))/2))
end if


if(mp_mpi) then
  write(*,*)
  write(*,*) "Info(Wannier): Mmn calculation"
end if

! loop over k and k+b points
ngrf=1 ! corresponds to expmt
! synchronise MPI processes
call mpi_barrier(mpicom,ierror)
! loop over reduced k-point set
call omp_hold(nkpt/np_mpi,nthd)
!$OMP PARALLEL DEFAULT(SHARED) &
!$OMP PRIVATE(ikp,n,m,inn,jk,bqvec,bqc,mmn,vkql) &
!$OMP PRIVATE(ngp,ngpq,igpig,igpqig) &
!$OMP PRIVATE(wfmt,wfir,wfmtq,wfirq) &
!$OMP PRIVATE(vgqc,gqc,jlgqr,ylmgq,sfacgq,expmt) &
!$OMP NUM_THREADS(nthd)

allocate(wfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfir(ngtot,nspinor,wann_nband))
allocate(jlgqr(njcmax,nspecies))
allocate(ylmgq(lmmaxo),sfacgq(natmtot))
allocate(expmt(npcmtmax,natmtot))
allocate(wfmtq(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfirq(ngtot,nspinor,wann_nband))
allocate(mmn(wann_nband,nspinor,wann_nband,nspinor))
allocate(igpig(ngkmax,nspnfv))
allocate(igpqig(ngkmax,nspnfv))
allocate(ngp(nspnfv),ngpq(nspnfv))

!$OMP DO
do ikp=1,nkpt

  call genwfsvp(.false.,.false.,wann_nband,wann_bands,ngridg,igfft,vkl(:,ikp),ngp,igpig,wfmt,ngtot,wfir)
  
  do inn=1,wann_nntot
    jk = nnlist(ikp,inn)
    bqvec=nncell(1:3,ikp,inn)+vkl(:,jk)-vkl(:,ikp)

    ! b-vector in Cartesian coordinates
    call r3mv(bvec,bqvec,bqc)
    ! generate the phase factor function exp(ib.r) in the muffin-tins
    call gengqrf(bqc,vgqc,gqc,jlgqr,ylmgq,sfacgq)
    call genexpmt(1,jlgqr,ylmgq,1,sfacgq,expmt)

    ! k+b-vector in lattice coordinates
    vkql = vkl(:,ikp)+bqvec
    call genwfsvp(.false.,.false.,wann_nband,wann_bands,ngridg,igfft,vkql,ngpq,igpqig,wfmtq,ngtot,wfirq)
    
    ! compute Mmn
    mmn = cmplx(0.d0,0.d0,kind=8)
    call genw90overlap(wfmt,wfir,wann_nband,nspinor,wfmtq,wfirq,mmn,expmt)

  !$OMP CRITICAL(genw90input_)
    ! write the Mmn matrix elements
    write(500,'(5I8)') ikp,nnlist(ikp,inn),nncell(:,ikp,inn)
    do n=1,wann_nband
      do m=1,wann_nband
        if (nspinor.eq.1) then
          write(500,'(2G18.10)') dble(mmn(m,1,n,1)),&
                                aimag(mmn(m,1,n,1))
        else
          write(500,'(2G18.10)') dble(mmn(m,1,n,1)+mmn(m,2,n,2)),&
                                aimag(mmn(m,1,n,1)+mmn(m,2,n,2))
        end if
      end do
    end do
  !$OMP END CRITICAL(genw90input_)

  end do ! end loop over b points

  ! compute spn
  if(nspinor.eq.2) then
    vec_0(1)=0.d0
    vec_0(2)=0.d0
    vec_0(3)=0.d0
    call gengqrf(vec_0,vgqc,gqc,jlgqr,ylmgq,sfacgq)
    call genexpmt(1,jlgqr,ylmgq,1,sfacgq,expmt)
    mmn = cmplx(0.d0,0.d0,kind=8)
    call genw90overlap(wfmt,wfir,wann_nband,nspinor,wfmt,wfir,mmn,expmt)
  !$OMP CRITICAL(genw90input_)
    is=1
    do m=1,wann_nband
      do n=1,m
        spn_x(ikp,is) = mmn(n,1,m,2)+mmn(n,2,m,1)
        spn_y(ikp,is) = cmplx(0.d0,1.d0,kind=8)*(mmn(n,2,m,1)-mmn(n,1,m,2))
        spn_z(ikp,is) = mmn(n,1,m,1)-mmn(n,2,m,2)
        is=is+1
      end do
    end do

  !$OMP END CRITICAL(genw90input_)

  end if
!$OMP CRITICAL(genw90input_)
  if (mp_mpi) then
    write(*,'("  Info(Wannier Mmn): completed ",I6," of ",I6," k-points")')&
                                                                   &ikp,nkpt
  end if
!$OMP END CRITICAL(genw90input_)

end do !End loop over k points
!$OMP END DO

deallocate(wfmt,wfir)
deallocate(jlgqr,ylmgq,sfacgq)
deallocate(igpig,igpqig)
deallocate(ngp,ngpq)
deallocate(expmt)
deallocate(wfmtq,wfirq)
deallocate(mmn)

!$OMP END PARALLEL
call omp_free(nthd)

call genw90amn

if (mp_mpi) then
  write(*,*)
  write(*,*) "Info(Wannier): Mmn and Amn for each k-point are computed"
end if

! close seedname.mmn
close(500)

! write seedname.spn
if(nspinor.eq.2) then
  do ikp=1,nkpt
    do is=1,(wann_nband*(wann_nband+1))/2
      write(502,'(2G18.10)') spn_x(ikp,is)
      write(502,'(2G18.10)') spn_y(ikp,is)
      write(502,'(2G18.10)') spn_z(ikp,is)
    end do
  end do

  ! close seedname.spn
  close(502)

  if(mp_mpi) then
    write(*,*)
    write(*,*) "Info(Wannier): matrix elements of S between Bloch states &
                                                             &are computed"
  end if
  deallocate(spn_x,spn_y,spn_z)
end if

!UNK
fmt_ikp = '(I5.5)' ! format of k-points in UNK filename an integer of width 5 with zeros at the left
fmt_is = '(I1.1)'

allocate(wfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfir(ngtot,nspinor,wann_nband))
allocate(zwfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(jlgqr(njcmax,nspecies))
allocate(ylmgq(lmmaxo),sfacgq(natmtot))
allocate(expmt(npcmtmax,natmtot))
allocate(zrhomt(npcmtmax,natmtot),zrhoir(ngtot))
allocate(igpig(ngkmax,nspnfv))
allocate(ngp(nspnfv))

!np = np3d(1)*np3d(2)*np3d(3)
!allocate(vpl(3,np),fp_real(np),fp_imag(np))
!! generate the 3D plotting points
!call plotpt3d(vpl)
!do ikp=1,nkpt
!  call genwfsvp(.true.,.false.,wann_nband,wann_bands,vkl(:,ikp),wfmt,ngtot,wfir)
!
!  do is = 1,nspinor
!    write (ikp_str,fmt_ikp) ikp ! converting integer to string using a 'internal file'
!    write (is_str,fmt_is) is ! converting integer to string using a 'internal file'
!    filename = 'UNK'//trim(ikp_str)//'.'//trim(is_str)
!    open(600,file=filename,action='WRITE',form='FORMATTED')
!
!    write(600,*) np3d(1),np3d(2),np3d(3),ikp,wann_nband
!    do m = 1,wann_nband
!      call genzrho(.true.,.true.,wfmt(:,:,is,m), wfir(:,is,m), &
!                                 wfmt(:,:,is,m), wfir(:,is,m), &
!                                                zrhomt, zrhoir)
!      ! evaluate the functions at the grid points
!      call rfplot(np,vpl,dreal(zrhomt),dreal(zrhoir),fp_real)
!      call rfplot(np,vpl,dimag(zrhomt),dimag(zrhoir),fp_imag)
!
!      ip = 0
!
!      do k=1,np3d(3)
!        do j=1,np3d(2)
!          do i=1,np3d(1)
!            ip = ip + 1
!            !ip = (j-1+(i-1)*np3d(2))*np3d(3) + k
!            write(600,'(2F7.3)') fp_real(ip),0.d0!fp_imag(ip)
!          end do
!        end do
!      end do
!
!    end do
!
!    close(600)
!  end do
!end do
ngrf=1
do ikp=1,nkpt

  call genwfsvp(.false.,.false.,wann_nband,wann_bands,ngridg,igfft,vkl(:,ikp),ngp,igpig,wfmt,ngtot,wfir)

  ! b-vector in Cartesian coordinates
  call r3mv(bvec,vkl(:,ikp),bqc)
  ! generate the phase factor function exp(ib.r) in the muffin-tins
  call gengqrf(bqc,vgqc,gqc,jlgqr,ylmgq,sfacgq)
  call genexpmt(1,jlgqr,ylmgq,1,sfacgq,expmt)
  do i=1,npcmtmax
    do j=1,natmtot
      do is = 1,nspinor
        do m = 1,wann_nband
          wfmt(i,j,is,m) = wfmt(i,j,is,m)*dconjg(expmt(i,j))
        end do
      end do
    end do
  end do
  
  do j=1,natmtot
    nrc=nrcmt(idxis(j))
    nrci=nrcmti(idxis(j))
    do is = 1,nspinor
      do m = 1,wann_nband
        call zfsht(nrc,nrci,wfmt(:,j,is,m),zwfmt(:,j,is,m))
      end do
    end do
  end do

  do is = 1,nspinor
    write (ikp_str,fmt_ikp) ikp ! converting integer to string using an 'internal file'
    write (is_str,fmt_is) is ! converting integer to string using an 'internal file'
    filename = 'UNK'//trim(ikp_str)//'.'//trim(is_str)
    open(600,file=filename,action='WRITE',form='FORMATTED')
    
    write(600,*) np3d(1),np3d(2),np3d(3),ikp,wann_nband
    do m = 1,wann_nband
      call plotUNK(600,zwfmt(:,:,is,m),wfir(:,is,m))
    end do

    close(600)
  end do

end do
deallocate(zwfmt,wfmt,wfir)
deallocate(jlgqr,ylmgq,sfacgq)
deallocate(igpig)
deallocate(ngp)

deallocate(wann_atomsymb,wann_atompos)
deallocate(nnlist,nncell)
deallocate(wann_proj_site)
deallocate(wann_proj_l,wann_proj_m,wann_proj_radial)
deallocate(wann_proj_zaxis,wann_proj_xaxis)
deallocate(wann_proj_zona)
deallocate(wann_proj_exclude_bands_lib)
deallocate(wann_proj_spin)
deallocate(wann_proj_quantdir)
deallocate(wann_proj_isrand)

if (mp_mpi) then
  write(*,*)
  write(*,*) "Info(Wannier): wannierization completed"
end if

reducek=reducek0

end subroutine genw90input
!EOC

! Copyright (C) 2015 Jon Lafuente and Manh Duc Le
! This file is distributed under the terms of the GNU General Public License.
! See the file COPYING for license details.

!BOP
! !ROUTINE: writew90mmn
! !INTERFACE:
subroutine writew90mmn
! !USES:
use modmain
use modw90
! !DESCRIPTION:
!   Writes the Mmn and Amn matrices required for Wannier90 to file.
!
! !REVISION HISTORY:
!   Created March 2015 (Jon Lafuente and Manh Duc Le)
!EOP
!BOC

implicit none
! local variables
integer ik,jk,n,m,igq0,ig,is,ias
integer innkp,ikp,inn,ik0,ispn
integer nrc,nrci,iro,irco,nrco,inc,ia,lm,l,ist,ir
integer reducek0,redkfil,nstsv_,recl
logical exists
real(8) t1
complex(8) :: z1
! allocatable arrays
real(8), allocatable :: vgqc(:,:),gqc(:)
complex(8), allocatable :: ylmgq(:,:),sfacgq(:,:)
complex(8), allocatable :: expmt(:,:,:)
complex(8), allocatable :: mmn(:,:,:,:),spn_x(:,:),spn_y(:,:),spn_z(:,:)
complex(8), allocatable :: wfmt(:,:,:,:,:),wfir(:,:,:)
complex(8), allocatable :: twfmt(:,:,:,:,:),twfir(:,:,:),twfmt1(:,:)
real(8), allocatable :: evalsv_(:)
! automatic arrays
real(8) :: bqvec(3),bqc(3)
character(256) filename
character(10) dat,tim
real(8) vkl_(3),vec_0(3)

! Checks that user has defined bands and projections
if(wann_projlines.eq.-1) then
  write(*,*)
  write(*,'("Error(writew90mmn): No projections specified - please input using the wann_projections block.")')
  write(*,*)
  stop
end if
if(wann_nband.eq.-1) then
  write(*,*)
  write(*,'("Error(writew90mmn): No bands specified - please input using the wann_bands block.")')
  write(*,*)
  stop
end if

! initialise global variables
call init0
! if reducek=1 was used in ground state calculations, need to regenerate the eigenvectors set for the full BZ. 
reducek0=reducek
if (reducek0.ne.0) reducek=0 
call init1
call init2
call init3

! get the nearest neighbour k-points and projections
call getw90nnkp
call getw90proj

! Open files for writting
filename = trim(wann_seedname)//'.mmn'
open(500,file=filename,action='WRITE',form='FORMATTED')
call date_and_time(date=dat,time=tim)
write(500,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
    dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
write(500,'(3I8)') wann_nband,nkpt,wann_nntot
filename = trim(wann_seedname)//'.amn'
open(501,file=filename,action='WRITE',form='FORMATTED')
call date_and_time(date=dat,time=tim)
write(501,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
    dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
if (nspinor.eq.1) then
  write(501,'(3I8)') wann_nband,nkpt,wann_nproj
else
  write(501,'(3I8)') wann_nband,nkpt,2*wann_nproj
  filename = trim(wann_seedname)//'.spn'
  open(502,file=filename,action='WRITE',form='FORMATTED')
  call date_and_time(date=dat,time=tim)
  write(502,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
      dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
  write(502,'(3I8)') wann_nband,nkpt
end if

! read density and potentials from file
call readstate
! read Fermi energy from a file
call readfermi
! find the new linearisation energies
call linengy
! generate the APW radial functions
call genapwfr
! generate the local-orbital radial functions
call genlofr
! generates the trial wavefunctions from the projection definitions read in
call genw90twf

! creates the trial wavefunction matrix
allocate(twfmt1(lmmaxvr,nrcmtmax))
allocate(twfmt(lmmaxvr,nrcmtmax,natmtot,nspinor,wann_nproj))
allocate(twfir(ngtot,nspinor,wann_nproj))
twfmt = cmplx(0.d0,0.d0,kind=8)
twfir = cmplx(0.d0,0.d0,kind=8)     ! trial wavefunction is zero outside muffin-tin.
inc=lmmaxvr*natmtot*nspinor*wann_nproj
do ispn=1,nspinor
  do is=1,nspecies
    nrc=nrcmt(is)
    nrci=nrcmtinr(is)
    iro=nrmtinr(is)+lradstp
    irco=nrci+1
    nrco=nrc-nrci
    do ia=1,natoms(is)
      ias=idxas(ia,is)
      do n=1,wann_nproj
        if(.not.wann_proj_haswt(n,ias)) cycle
        ! zero the wavefunction
        twfmt1 = cmplx(0.d0,0.d0,kind=8)
        lm=0
        do l=0,lmaxinr
          do m=-l,l
            lm=lm+1
            z1=wann_projclm(lm,ias,n)
            if(abs(z1).gt.1.d-14) then
!             call zaxpy(nrc,z1,wann_projulr(1,l,ias,n),lradstp,twfmt1(lm,1),lmmaxvr)
              do ir=1,nrc
                twfmt1(lm,ir)=twfmt1(lm,ir)+z1*wann_projulr(ir,l,ias,n)
              end do
            end if
          end do
        end do
        do l=lmaxinr+1,lmaxvr
          do m=-l,l
            lm=lm+1
            z1=wann_projclm(lm,ias,n)
            if(abs(z1).gt.1.d-14) then
!             call zaxpy(nrco,z1,wann_projulr(iro,l,ias,n),lradstp,twfmt1(lm,irco),lmmaxvr)
              do ir=irco,nrc
                twfmt1(lm,ir)=twfmt1(lm,ir)+z1*wann_projulr(ir,l,ias,n)
              end do
            end if
          end do
        end do
        call zbsht(nrc,nrci,twfmt1,twfmt(:,:,ias,ispn,n))
      end do
    end do
  end do
end do
deallocate(twfmt1)

! check that EVECSV.OUT has all necessary k-points
allocate(evalsv_(nstsv))
redkfil=0
inquire(iolength=recl) vkl_,nstsv_,evalsv_
do ik=1,nkpt
  exists=.false.
  t1=9.d99
  inquire(file='EVALSV'//trim(filext),exist=exists)
  if(exists) then
    open(70,file='EVALSV'//trim(filext),action='READ',form='UNFORMATTED', &
        access='DIRECT',recl=recl,err=101)
    read(70,rec=ik,err=101) vkl_,nstsv_,evalsv_
    close(70)
    t1=abs(vkl(1,ik)-vkl_(1))+abs(vkl(2,ik)-vkl_(2))+abs(vkl(3,ik)-vkl_(3))
  end if
101 continue
  if (.not.exists.or.t1.gt.epslat.or.nstsv.ne.nstsv_) then
    redkfil=1
    exit
  end if
end do
! If kpoint not found in saved eigen-values/vectors, then need to recompute EVEC*OUT.
if (redkfil.ne.0) then
  write(*,'("Info(writew90mmn): saved k-points do not contain all required k-points. &
           &Recalculating wavefunctions")')
! compute the overlap radial integrals
  call olprad
! compute the Hamiltonian radial integrals
  call hmlrad
! generate the spin-orbit coupling radial functions
  call gensocfr
! generate the first- and second-variational eigenvectors and eigenvalues
  call genevfsv
end if

! write .eig file for wannier90
filename = trim(wann_seedname)//'.eig'
open(50,file=filename,action='WRITE',form='FORMATTED')
do ik=1,nkpt
  call getevalsv(filext,vkl(:,ik),evalsv_)
  do ig=1,wann_nband
    ist=wann_bands(ig)
    write(50,'(2I12,G18.10)') ig,ik,(evalsv_(ist)-efermi)*27.211385  ! convert to eV
  end do
end do
close(50)
deallocate(evalsv_)

if(nspinor.eq.2) then
  allocate(spn_x(nkpt,(wann_nband*(wann_nband+1))/2))
  allocate(spn_y(nkpt,(wann_nband*(wann_nband+1))/2))
  allocate(spn_z(nkpt,(wann_nband*(wann_nband+1))/2))
end if

!Loop over k and k+b points
!$OMP PARALLEL DEFAULT(SHARED) &
!$OMP PRIVATE(ikp,innkp,inn,ik,jk,bqvec,vgqc,gqc,ylmgq,sfacgq,mmn,ik0,wfmt,wfir,expmt)
!$OMP DO
do ikp=1,nkpt
  allocate(wfmt(lmmaxvr,nrcmtmax,natmtot,nspinor,wann_nband))
  allocate(wfir(ngtot,nspinor,wann_nband))
  ik0=-1
  do inn=1,wann_nntot
    innkp=inn+(ikp-1)*wann_nntot
    ik=wann_nnkp(1,innkp)
    jk=wann_nnkp(2,innkp)
    bqvec=wann_nnkp(3:5,innkp)+vkl(:,jk)-vkl(:,ik)
    
    if(ik.ne.ik0) then
      ik0=ik
      call genwfsvp(.false.,.false.,wann_nband,wann_bands,vkl(:,ik),wfmt,ngtot,wfir)
    end if

    ! generate the G+q-vectors and related quantities
    allocate(vgqc(3,ngrf),gqc(ngrf))
    allocate(ylmgq(lmmaxvr,ngrf),sfacgq(ngrf,natmtot))
    call gengqrf(vecqc,igq0,vgqc,gqc,ylmgq,sfacgq)
    deallocate(vgqc)

    allocate(expmt(lmmaxvr,nrcmtmax,natmtot))
    ! b-vector in Cartesian coordinates
    call r3mv(bvec,bqvec,bqc)
    ! generate the phase factor function exp(ib.r) in the muffin-tins
    call genexpmt(bqc,expmt)

    ! compute Mmn
    allocate(mmn(wann_nband,4,wann_nband,4))
    mmn(:,:,:,:)=cmplx(0.d0,0.d0,kind=8)
    call genw90mmn(ik,bqvec,expmt,gqc,ylmgq,sfacgq,mmn,wfmt,wfir)
    deallocate(expmt)

!$OMP CRITICAL
    !Write the Mmn matrix elements
    write(500,'(5I8)') wann_nnkp(:,innkp)
    do n=1,wann_nband
      do m=1,wann_nband
        if (nspinor.eq.1) then
          write(500,'(2G18.10)') dble(mmn(m,1,n,1)),aimag(mmn(m,1,n,1))
        else
          write(500,'(2G18.10)') dble(mmn(m,1,n,1)+mmn(m,2,n,2)),aimag(mmn(m,1,n,1)+mmn(m,2,n,2))
          !write(502,'(3I8,2G18.10)') m,n,ikp,dble(mmn(m,1,n,1)-mmn(m,2,n,2)),aimag(mmn(m,1,n,1)-mmn(m,2,n,2))
        end if
      end do
    end do
    write(*,'("Info(writew90mmn): completed ",I5," of ",I5," Mmn(k,k+b) points")')& 
        innkp,wann_nntot*nkpt
!$OMP END CRITICAL
    deallocate(mmn)

!Write the .spn file
    if(inn.eq.1) then
      if(nspinor.eq.2) then
        allocate(mmn(wann_nband,4,wann_nband,4))
        allocate(expmt(lmmaxvr,nrcmtmax,natmtot))
        vec_0(1)=0.d0
        vec_0(2)=0.d0
        vec_0(3)=0.d0
        call genexpmt(vec_0,expmt)
        mmn(:,:,:,:)=cmplx(0.d0,0.d0,kind=8)
        call genw90mmn(ik,vec_0,expmt,gqc,ylmgq,sfacgq,mmn,wfmt,wfir)
!$OMP CRITICAL
        is=1
        do m=1,wann_nband
          do n=1,m
           !write(502,'(3I8,2G18.10)') m,n,ikp,dble(mmn(m,1,n,1)-mmn(m,2,n,2)),aimag(mmn(m,1,n,1)-mmn(m,2,n,2))
           !write(502,'(2G18.10)')  dble(mmn(n,1,m,2)+mmn(n,2,m,1)),aimag(mmn(n,1,m,2)+mmn(n,2,m,1)) ! x
           !write(502,'(2G18.10)') -aimag(mmn(n,2,m,1)-mmn(n,1,m,2)),dble(mmn(n,2,m,1)-mmn(n,1,m,2)) ! y
           !write(502,'(2G18.10)')  dble(mmn(n,1,m,1)-mmn(n,2,m,2)),aimag(mmn(n,1,m,1)-mmn(n,2,m,2)) ! z
            spn_x(ikp,is) = mmn(n,1,m,2)+mmn(n,2,m,1) 
            spn_x(ikp,is) = cmplx(0.d0,1.d0,kind=8)*(mmn(n,2,m,1)-mmn(n,1,m,2))
            spn_z(ikp,is) = mmn(n,1,m,1)-mmn(n,2,m,2) 
            is=is+1
          end do
        end do
!$OMP END CRITICAL
        deallocate(mmn)
        deallocate(expmt)
      end if
    end if

! Calculates the overlap integrals Amn(k)
    if(inn.eq.1) then
      allocate(mmn(wann_nband,4,wann_nproj,4))
      mmn(:,:,:,:)=cmplx(0.d0,0.d0,kind=8)
      call genw90amn(wfmt,wfir,twfmt,twfir,gqc,ylmgq,sfacgq,mmn)
!$OMP CRITICAL
    if (nspinor.eq.1) then
      do n=1,wann_nproj
        do m=1,wann_nband
            write(501,'(3I8,2G18.10)') m,n,ikp,dble(mmn(m,1,n,1)),aimag(mmn(m,1,n,1))
        end do
      end do
    else
      do n=1,2*wann_nproj,2
        do m=1,wann_nband
            write(501,'(3I8,2G18.10)') m,n,ikp,dble(mmn(m,1,n,1)),aimag(mmn(m,1,n,1))
            write(501,'(3I8,2G18.10)') m,n+1,ikp,dble(mmn(m,2,n,1)),aimag(mmn(m,2,n,1))
        end do
      end do
    end if
    write(*,'("Info(writew90mmn): completed ",I5," of ",I5," Amn(k) points")')&
        ik,nkpt
!$OMP END CRITICAL
      deallocate(mmn)
    end if

    deallocate(gqc,ylmgq,sfacgq)

  end do
  deallocate(wfmt,wfir)
!End loop over k, k+b points
end do
!$OMP END DO
!$OMP END PARALLEL

close(500)
close(501)

if(nspinor.eq.2) then
  do ikp=1,nkpt
    do is=1,(wann_nband*(wann_nband+1))/2
      write(502,'(2G18.10)') spn_x(ikp,is)
      write(502,'(2G18.10)') spn_y(ikp,is)
      write(502,'(2G18.10)') spn_z(ikp,is)
    end do
  end do
  close(502)
  deallocate(spn_x,spn_y,spn_z)
end if

reducek=reducek0

end subroutine writew90mmn
!EOC


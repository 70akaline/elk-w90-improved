
! Copyright (C) 2017-18 Arsenii Gerasimov, Yaroslav Kvashnin and Lars Nordstrom.
! This file is distributed under the terms of the GNU General Public License.
! See the file COPYING for license details.

!BOP
! !ROUTINE: genw90amn
! !INTERFACE:
subroutine genw90amn
! !USES:
use modmain
use modw90
use modw90overlap
use modmpi
use modomp
! !DESCRIPTION:
!   Calculates the Amn overlap matrices between Bloch states and
!   trial orbitals required for Wannier90.
!
! !REVISION HISTORY:
!   Created June 2017 (Arsenii Gerasimov)
!EOP
!BOC
implicit none
! local variables
logical :: wann_spn_up = .false., wann_spn_dn = .false.
integer ikp,n,m,nthd
! allocatable arrays
integer,    allocatable :: igpig(:,:)
complex(8), allocatable :: wfmt(:,:,:,:),wfir(:,:,:)
complex(8), allocatable :: twfmt(:,:,:,:),twfir(:,:,:)
complex(8), allocatable :: amn(:,:,:,:)
! automatic arrays
integer        ngp(nspnfv)
character(256) filename
character(10)  dat,tim
!-------------------------------------------------------------------------------
!
call initw90

if ( mp_mpi ) then
  ! Open Amn-file
  filename = trim(wann_seedname)//'.amn'
  open(501,file=filename,action='WRITE',form='FORMATTED')
  call date_and_time(date=dat,time=tim)
  ! Amn-file header
  write(501,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                           dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
  write(501,'(3I8)') wann_nband,nkpt,wann_nproj
end if

if ( mp_mpi ) then
  write(*,*)
  write(*,*) " Info(Wannier): Calculating trial wavefunctions"
end if

allocate(twfmt(npcmtmax,natmtot,nspinor,wann_nproj))
allocate(twfir(ngtot,nspinor,wann_nproj))
! Generate trial wavefunctions from the specified projections in Wannier-block
call genw90twf(twfmt,twfir)

if ( mp_mpi ) then
  write(*,*)
  write(*,'("  Info(Wannier): Trial wavefunctions have been generated from the first APW in the list")')
  write(*,'("    List of projections (",i3,"):")') wann_nproj

  do n = 1,wann_nproj
    if ( nspinor .eq. 2 ) then
      write(*,'("      n=",i3," : R_at =(",3f10.5,") ; l=", i2," ; m=", i2, " ; ms=", i2)') &
                                                    n, wann_proj_site(:,n), wann_proj_l(n), &
                                                          wann_proj_m(n), wann_proj_spin(n)
    else
      write(*,'("      n=",i3," : R_at =(",3f10.5,") ; l=", i2," ; m=", i2)') &
                                      n, wann_proj_site(:,n), wann_proj_l(n), &
                                                               wann_proj_m(n)
    end if
  end do

  ! Special check for the case when the projection is done on a single spin
  ! channel in spin-pol calculation without SOC
  ! (projections on both spins can be done independently)
  ! In this case, the projections spins have to be consistent with the band
  ! indices
    if ( spinpol .and. .not.(spinorb) ) then
      if ( any( wann_proj_spin .eq. 1)  ) wann_spn_up = .true.
      if ( any( wann_proj_spin .eq. -1) ) wann_spn_dn = .true.
      ! First half of bands have pure spin-up character
      if ( wann_spn_up .and. .not.(wann_spn_dn) .and. any( wann_bands(1:wann_nband).gt.int(nstsv/2) ) ) then
        write(*,*)
        write(*,'("Error(Wannier): Projecting on the spin-up states ...")')
        write(*,'("                You are trying to project spin-up bands onto spin-dn trial wavefunction")')
        write(*,'("                Change the spin-(u) character of the projection ")')
        stop
      end if
      ! Second half of bands have pure spin-dn character
      if (.not.(wann_spn_up) .and. wann_spn_dn .and. any( wann_bands(1:wann_nband).le.int(nstsv/2) ) ) then
        write(*,*)
        write(*,'("Error(Wannier): Projecting on the spin-dn states ...")')
        write(*,'("                You are trying to project spin-up bands onto spin-dn trial wavefunction")')
        write(*,'("                Change the spin-(d) character of the projection ")')
        stop
      end if
    end if

    write(*,*)
    write(*,*) " Info(Wannier): Amn calculation"
end if

! Synchronise MPI processes
call mpi_barrier(mpicom,ierror)
! Loop over k and k+b points
call omp_hold(nkpt/np_mpi,nthd)
!$OMP PARALLEL DEFAULT(SHARED)&
!$OMP PRIVATE(ikp,n,m,ngp,igpig,amn,wfmt,wfir)&
!$OMP NUM_THREADS(nthd)

allocate(wfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfir(ngtot,nspinor,wann_nband))
allocate(amn(wann_nband,nspinor,wann_nproj,nspinor))
allocate(igpig(ngkmax,nspnfv))

!$OMP DO
do ikp = 1,nkpt
  call genwfsvp(.false.,.false.,wann_nband,wann_bands,ngridg,igfft,vkl(:,ikp),ngp,igpig,wfmt,ngtot,wfir)

  ! Calculate the overlap integrals Amn(k)
  amn = cmplx(0.d0,0.d0,kind=8)
  call genw90overlap(wfmt,wfir,wann_nproj,twfmt,twfir,amn)
!$OMP CRITICAL(genw90amn_)
  ! Write the Amn matrix elements
  do n = 1,wann_nproj
    do m = 1,wann_nband
      if ( nspinor .eq. 1 ) then
        write(501,'(3I8,2G18.10)') m,    n,ikp,dble(amn(m,1,n,1)),&
                                              aimag(amn(m,1,n,1))
      else
         write(501,'(3I8,2G18.10)') m,   n,ikp,dble(amn(m,1,n,1) + amn(m,2,n,2)),&
                                              aimag(amn(m,1,n,1) + amn(m,2,n,2))
      end if
    end do
  end do

  if ( mp_mpi ) then
    write(*,'("    Info(Wannier Amn): completed ",I6," of ",I6," k-points")')&
                                                                    &ikp,nkpt
  end if
!$OMP END CRITICAL(genw90amn_)

end do !End loop over k points
!$OMP END DO

deallocate(igpig)
deallocate(wfmt,wfir)
deallocate(amn)

!$OMP END PARALLEL
call omp_free(nthd)

deallocate(twfmt,twfir)

! Close <seedname>.amn
close(501)

reducek = reducek0

end subroutine
!EOC

! Copyright (C) 2018 Arsenii Gerasimov
! This file is distributed under the terms of the GNU General Public
! License.
! See the file COPYING for license details.

!BOP
! !ROUTINE: genw90amn
! !INTERFACE:
subroutine genw90amn
! !USES:
use modmain
use modw90
use modw90overlap
use modmpi
use modomp
! !DESCRIPTION:
!   Calculates the Amn overlap matrices between Bloch states and
!   trial orbitals required for Wannier90.
!EOP
!BOC
implicit none
! local variables
integer is,ia,ikp,n,m
integer nst,nthd
integer redkfil,nstsv_,recl
logical exists
real(8) t1
! automatic arrays
character(256)    filename
character(10)     dat,tim
integer           ngp(nspnfv)
real(8)           vkl_(3)
! external functions

! allocatable arrays
integer,    allocatable :: igpig(:,:)
complex(8), allocatable :: wfmt(:,:,:,:),wfir(:,:,:)
complex(8), allocatable :: twfmt(:,:,:,:),twfir(:,:,:)
complex(8), allocatable :: amn(:,:,:,:)
real(8),    allocatable :: evalsv_(:)

reducek0=reducek ! if reducek=1 was used in ground state calculations,
                 ! need to regenerate the eigenvectors set for the full BZ.
reducek=0

call setupw90lib

if (mp_mpi) then
 ! open Amn-file
 filename = trim(wann_seedname)//'.amn'
 open(501,file=filename,action='WRITE',form='FORMATTED')
 call date_and_time(date=dat,time=tim)
 write(501,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                           dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
 ! Amn-file header
   write(501,'(3I8)') wann_nband,nkpt,wann_nproj
end if

! read density and potentials from file
call readstate
! Fourier transform Kohn-Sham potential to G-space
call genvsig
! read Fermi energy from a file
call readfermi
! find the new linearisation energies
call linengy
! generate the APW radial functions
call genapwfr
! generate the local-orbital radial functions
call genlofr

! check that EVECSV.OUT has all necessary k-points
allocate(evalsv_(nstsv))
redkfil=0
inquire(iolength=recl) vkl_,nstsv_,evalsv_
do ikp=1,nkpt
  exists=.false.
  t1=9.d99
  inquire(file='EVALSV'//trim(filext),exist=exists)
  if(exists) then
    open(70,file='EVALSV'//trim(filext),action='READ',form='UNFORMATTED', &
        access='DIRECT',recl=recl,err=101)
    read(70,rec=ikp,err=101) vkl_,nstsv_,evalsv_
    close(70)
    t1=abs(vkl(1,ikp)-vkl_(1))+abs(vkl(2,ikp)-vkl_(2))+abs(vkl(3,ikp)-vkl_(3))
  end if
101 continue
  if (.not.exists.or.t1.gt.epslat.or.nstsv.ne.nstsv_) then
    redkfil=1
    exit
  end if
end do
! If kpoint not found in saved eigen-values/vectors, then need to recompute
! EVEC*OUT.
if (redkfil.ne.0) then
! compute the overlap radial integrals
  call olprad
! compute the Hamiltonian radial integrals
  call hmlrad
! generate the spin-orbit coupling radial functions
  call gensocfr
! generate the first- and second-variational eigenvectors and eigenvalues
  call genevfsv  
end if

! generates the trial wavefunctions from the projection definitions read in
allocate(twfmt(npcmtmax,natmtot,nspinor,wann_nproj))
allocate(twfir(ngtot,nspinor,wann_nproj))

call genw90twf(twfmt,twfir)

if (mp_mpi) then
  write(*,*) 
  write(*,*) " Info(Wannier): Trial wavefunctions have been generated from the first APW in the list"
  write(*,*) 
  write(*,'("  List of projections (",i3,"):")')  wann_nproj

  do n=1,wann_nproj
   if (nspinor .eq. 2) then   
    write(*,'("    n=",i3," : R_at =(",3f10.5,") ; l=", i2," ; m=", i2, " ; ms=", i2)') n, wann_proj_site(:,n), wann_proj_l(n), wann_proj_m(n), wann_proj_spin(n)
   else
    write(*,'("    n=",i3," : R_at =(",3f10.5,") ; l=", i2," ; m=", i2)') n, wann_proj_site(:,n), wann_proj_l(n), wann_proj_m(n) 
   end if
  end do

  write(*,*)
  write(*,*) " Info(Wannier): Calculating trial wavefunctions"

  write(*,*) 
  write(*,*) " Info(Wannier): Amn calculation"
end if

! synchronise MPI processes
call mpi_barrier(mpicom,ierror)
! loop over k and k+b points
call omp_hold(nkpt/np_mpi,nthd)
!$OMP PARALLEL DEFAULT(SHARED)&
!$OMP PRIVATE(ikp,n,m,ngp,igpig,amn,wfmt,wfir)&
!$OMP NUM_THREADS(nthd)

allocate(wfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfir(ngtot,nspinor,wann_nband))
allocate(amn(wann_nband,nspinor,wann_nproj,nspinor))
allocate(igpig(ngkmax,nspnfv))

!$OMP DO
do ikp=1,nkpt
  call genwfsvp(.false.,.false.,wann_nband,wann_bands,ngridg,igfft,vkl(:,ikp),ngp,igpig,wfmt,ngtot,wfir)
 
  ! calculate the overlap integrals Amn(k)
  amn = cmplx(0.d0,0.d0,kind=8)
  call genw90overlap(wfmt,wfir,wann_nproj,twfmt,twfir,amn)
!$OMP CRITICAL(genw90amn_)
  ! write the Amn matrix elements
  do n=1,wann_nproj
    do m=1,wann_nband
      if (nspinor.eq.1) then
        write(501,'(3I8,2G18.10)') m,    n,ikp,dble(amn(m,1,n,1)),&
                                                aimag(amn(m,1,n,1))
      else
         write(501,'(3I8,2G18.10)') m,   n,ikp,dble(amn(m,1,n,1)+amn(m,2,n,2)),&
                                           aimag(amn(m,1,n,1)+amn(m,2,n,2))
      end if
    end do
  end do

  if (mp_mpi) then
    write(*,'("  Info(Wannier Amn): completed ",I6," of ",I6," k-points")')&
                                                                   &ikp,nkpt
  end if  
!$OMP END CRITICAL(genw90amn_)

end do !End loop over k points
!$OMP END DO

deallocate(igpig)
deallocate(wfmt,wfir)
deallocate(amn)

!$OMP END PARALLEL
call omp_free(nthd)

deallocate(twfmt,twfir)

! close seedname.amn
close(501)

reducek=reducek0

end subroutine
!EOC


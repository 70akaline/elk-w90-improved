! Copyright (C) 2018 Arsenii Gerasimov
! This file is distributed under the terms of the GNU General Public
! License.
! See the file COPYING for license details.

!BOP
! !ROUTINE: genw90amn
! !INTERFACE:
subroutine genw90amn
! !USES:
use modmain
use modw90
use modw90overlap
! !DESCRIPTION:
!   Calculates the Amn overlap matrices between Bloch states and
!   trial orbitals required for Wannier90.
!EOP
!BOC
implicit none
! local variables
integer is,ia,ikp,n,m
! local variables

! automatic arrays
character(256)    filename
character(10)     dat,tim
! external functions

! allocatable arrays
complex(8), allocatable :: wfmt(:,:,:,:),wfir(:,:,:)
complex(8), allocatable :: twfmt(:,:,:,:),twfir(:,:,:)
complex(8), allocatable :: amn(:,:,:,:)

reducek0=reducek ! if reducek=1 was used in ground state calculations,
                 ! need to regenerate the eigenvectors set for the full BZ.
reducek=0

call setupw90lib

! open Amn-file
filename = trim(wann_seedname)//'.amn'
open(501,file=filename,action='WRITE',form='FORMATTED')
call date_and_time(date=dat,time=tim)
write(501,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                          dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)

! Amn-file header
if (nspinor.eq.1) then
  write(501,'(3I8)') wann_nband,nkpt,wann_nproj
else
  write(501,'(3I8)') wann_nband,nkpt,wann_nwf
end if

! read density and potentials from file
call readstate
! read Fermi energy from a file
call readfermi
! find the new linearisation energies
call linengy
! generate the APW radial functions
call genapwfr
! generate the local-orbital radial functions
call genlofr
! generates the trial wavefunctions from the projection definitions read in
allocate(twfmt(npcmtmax,natmtot,nspinor,wann_nproj))
allocate(twfir(ngtot,nspinor,wann_nproj))
call genw90twf(twfmt,twfir)

! loop over k and k+b points
!$OMP PARALLEL DEFAULT(SHARED) &
!$OMP PRIVATE(ikp,n,m,amn,wfmt,wfir)

allocate(wfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfir(ngtot,nspinor,wann_nband))
allocate(amn(wann_nband,nspinor,wann_nproj,1))

!$OMP DO
do ikp=1,nkpt
  call genwfsvp(.false.,.false.,wann_nband,wann_bands,vkl(:,ikp),wfmt,ngtot,wfir)
 
  ! calculate the overlap integrals Amn(k)
  amn = cmplx(0.d0,0.d0,kind=8)
  call genw90overlap(wfmt,wfir,wann_nproj,1,twfmt,twfir,amn)
!$OMP CRITICAL
  ! write the Amn matrix elements
  do n=1,wann_nproj
    do m=1,wann_nband
      if (nspinor.eq.1) then
        write(501,'(3I8,2G18.10)') m,    n,ikp,dble(amn(m,1,n,1)),&
                                                aimag(amn(m,1,n,1))
      else
        write(501,'(3I8,2G18.10)') m,2*n-1,ikp,dble(amn(m,1,n,1)),&
                                                aimag(amn(m,1,n,1))
        write(501,'(3I8,2G18.10)') m,  2*n,ikp,dble(amn(m,2,n,1)),&
                                                aimag(amn(m,2,n,1))
      end if
    end do
  end do
!$OMP END CRITICAL 

end do !End loop over k points
!$OMP END DO

deallocate(wfmt,wfir)
deallocate(amn)

!$OMP END PARALLEL

deallocate(twfmt,twfir)

! close seedname.amn
close(501)

reducek=reducek0

end subroutine
!EOC

